!   Record editor

    script Editor
    
    import div Container
    
    div Form
    div Title
    div Row
    div Input
    div Parents
    div Topics
    div Buttons
    div ButtonGroup
    div RecordID
    span Label
    input GivenName
    input OtherNames
    input FamilyName
    input GenderM
    input GenderF
    input GenderO
    button NewRecordButton
    button ViewRecordButton
    button CancelButton
    variable Index
    variable Item
    variable Record
    variable ID
    variable G
    variable A
    variable B
    variable V
    variable Script
    module ParentsModule
    module TopicsModule

!	Set up the screen
Setup:
	clear Container
    
    create Form in Container
    set the style of Form to
    	`width:100%;max-width:800px;height:calc(100vh - 2em)`
        cat `;margin:1em auto 0 auto;border:1px solid lightgray;padding:0 0.5em 0.5em 0.5em`

    create Title in Form
    set the style of Title to `text-align:center;font-size:1.1em;font-weight:bold;margin-top:0.5em`
    set the content of Title to `Record editor`

	create Row in Form
    set the class of Row to `rowstyle`
    create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Record ID:`
    create Input in Row
    set the class of Input to `inputstyle`
    create RecordID in Input
    set the style of RecordID to `width:100%`

	create Row in Form
    set the class of Row to `rowstyle`
    create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Given name:`
    create Input in Row
    set the class of Input to `inputstyle`
    create GivenName in Input
    set the style of GivenName to `width:100%`

	create Row in Form
    set the class of Row to `rowstyle`
    create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Other names:`
    create Input in Row
    set the class of Input to `inputstyle`
    create OtherNames in Input
    set the style of OtherNames to `width:100%`

	create Row in Form
    set the class of Row to `rowstyle`
    create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Family name:`
    create Input in Row
    set the class of Input to `inputstyle`
    create FamilyName in Input
    set the style of FamilyName to `width:100%`
    
    create Row in Form
    set the class of Row to `rowstyle`
    create Label in Row
    set the class of Label to `labelstyle`
    set the content of Label to `Gender`
    create ButtonGroup in Row
    create GenderM in ButtonGroup
    set the style of GenderM to `display:inline-block`
    set attribute `type` of GenderM to `radio`
    set attribute `name` of GenderM to `gender`
    set attribute `value` of GenderM to `m`
    create Label in ButtonGroup
    set the content of Label to `&nbsp;Male`
    create GenderF in ButtonGroup
    set the style of GenderF to `display:inline-block`
    set attribute `type` of GenderF to `radio`
    set attribute `name` of GenderF to `gender`
    set attribute `value` of GenderF to `f`
    create Label in ButtonGroup
    set the content of Label to `&nbsp;Female`
    create GenderO in ButtonGroup
    set the style of GenderO to `display:inline-block`
    set attribute `type` of GenderO to `radio`
    set attribute `name` of GenderO to `gender`
    set attribute `value` of GenderO to `o`
    create Label in ButtonGroup
    set the content of Label to `&nbsp;Other`

	create Parents in Form
    create Topics in Form
    
!	The line of buttons under the form

    create Buttons in Form
    set the style of Buttons to
    	`text-align:center;margin-top:0.5em;border-top:1px solid lightgray;padding-top:0.5em`

    create ViewRecordButton in Buttons
    set the style of ViewRecordButton to `margin-left:0.5em`
    set the text of ViewRecordButton to `Save/View record`
    on click ViewRecordButton
    begin
        go to ViewRecord
    end
    
    create NewRecordButton in Buttons
    set the style of NewRecordButton to `margin-left:0.5em`
    set the text of NewRecordButton to `New record`
    on click NewRecordButton
    begin
        go to NewRecord
    end
    
    create CancelButton in Buttons
    set the style of CancelButton to `margin-left:0.5em`
    set the text of CancelButton to `Cancel`
    on click CancelButton
    begin
    	rest get Record from `/people/P` cat ID cat `.json` or stop
        go to Cancel
    end
    
    on message
    begin
      	put the message into ID
    	if ID is `-` put `` cat now into ID
        set Record to object
       	rest get Record from `/people/P` cat ID cat `.json` or stop
        set the content of RecordID to ID
        put property `gname` of Record into Item
        set the content of GivenName to Item
        put property `fname` of Record into Item
        set the content of FamilyName to Item
        put property `oname` of Record into Item
        set the content of OtherNames to Item
        put property `gender` of Record into G
        if G is `m` set attribute `checked` of GenderM to `checked`
        else if G is `f` set attribute `checked` of GenderF to `checked`
        else set attribute `checked` of GenderO to `checked`
    	clear Parents
        rest get Script from `/ecs/parents.ecs`
        run Script with Parents and Record as ParentsModule
        send to ParentsModule
    	clear Topics
        rest get Script from `/ecs/edittopics.ecs`
        run Script with Topics and Record as TopicsModule
        send to TopicsModule
	end

	set ready
	stop

!	View the current record
ViewRecord:
    if GivenName is empty go to MissingData
    gosub to SaveRecord
Cancel:
	if ParentsModule is running send `exit` to ParentsModule
	if TopicsModule is running send `exit` to TopicsModule
	send ID to parent
	exit

!	Create a new record
NewRecord:
    if GivenName is empty go to MissingData
    gosub to SaveRecord
    clear GivenName
    clear OtherNames
    clear FamilyName
    send `clear` to ParentsModule
    set Record to object
    put now into ID
    stop

!	Save the current record
SaveRecord:
    if GivenName is empty stop
    ! Write the record
    set property `gname` of Record to GivenName
    set property `oname` of Record to OtherNames
    set property `fname` of Record to FamilyName
    if attribute `checked` of GenderM put `m` into G
    else if attribute `checked` of GenderF put `f` into G
    else put `o` into G
    set property `gender` of Record to G
    rest post Record to `save/people/P` cat ID cat `.json`
    rest get Script from `/ecs/map.ecs`
    run Script
    print `Record for '` cat GivenName cat ` ` cat FamilyName cat `' saved`
	return

MissingData:
	alert `Please provide the given name.`
    stop

!	Function to sort the index
SortIndex:
    put arg `a` of Index into A
    put arg `b` of Index into B
    if A is greater than B put 1 into V
    else if A is less than B put -1 into V
    else put 0 into V
    set arg `v` of Index to V
    stop