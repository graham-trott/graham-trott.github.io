!   Record viewer

    script Viewer
    
    div Body
    div Container
    div ViewerPanel
    div EditorPanel
    div Row
    div Choices
    fieldset Fieldset
    legend Legend
    span Label
    img Top
    img Mid
    img Bottom
    img SearchButton
    img EditButton
    h1 Title
    p Para
    input Name
    a ChildLink
    a SiblingLink
    a Link
    button ChoiceButton
    button FindButton
    button CancelButton
    module EditorModule
    variable Editable
    variable ID
    variable Index
    variable Map
    variable Record
    variable Record2
    variable Item
    variable GivenName
    variable OtherNames
    variable FamilyName
    variable Date
    variable FID
    variable MID
    variable Father
    variable Mother
    variable Child
    variable Sibling
    variable Gender
    variable List
    variable Text
    variable Script
    variable Selection
    variable ChoiceIndex
    variable N
    
    get Editable from storage as `editable`
    if Editable is `true` set Editable else clear Editable
    
    clear body
    create Body in body
    set the style of Body to `position:relative;width:calc(100vw - 20px);height:calc(100vh - 20px)`
    create ViewerPanel in Body
    set the style of ViewerPanel to `width:100%;height:100%`
    
    create Mid in ViewerPanel
    set the style of Mid to `width:100%;height:100%;position:relative`
    set attribute `src` of Mid to `images/parchment-mid.png`
    
    create Top in ViewerPanel
    set the style of Top to `width:100%;height:10vh;position:absolute;left:0;top:0`
    set attribute `src` of Top to `images/parchment-top.png`
    
    create Bottom in ViewerPanel
    set the style of Bottom to `width:100%;height:10vh;position:absolute;left:0;bottom:0`
    set attribute `src` of Bottom to `images/parchment-bottom.png`
    
    create Container in ViewerPanel
    set the style of Container to
    	`width:calc(80vw - 20px);height:calc(80vh - 20px);position:absolute;top:10vh;left:10vw`

	create SearchButton in ViewerPanel
    set the style of SearchButton to `width:3vw;height:3vw;position:absolute;top:10vh;right:11vw`
    set attribute `src` of SearchButton to `icons/binoculars.png`
    on click SearchButton go to Search
    
    create EditorPanel in Body
    set style `display` of EditorPanel to `none`

	if Editable
    begin
		create EditButton in ViewerPanel
	    set the style of EditButton to `width:3vw;height:3vw;position:absolute;top:10vh;right:15vw`
	    set attribute `src` of EditButton to `icons/edit.png`
	    on click EditButton go to EditRecord
    end
    
    on message
    begin
    	set style `display` of ViewerPanel to `block`
        set style `display` of EditorPanel to `none`
        if Editable set style `display` of EditButton to `block`
        set style `display` of SearchButton to `block`
        put the message into ID
        go to ViewRecord
    end

	set ready
    wait 50 ticks
    if Editable
    begin
    	gosub to StartEditor
    	if the json count of Index is 0 go to EditRecord
    end
    stop    

StartEditor:
    if EditorModule is not running
    begin
	    rest get Script from `/ecs/editor.ecs`
	    run Script with EditorPanel as EditorModule
    end
    return

EditRecord:
   	gosub to StartEditor
   	set style `display` of ViewerPanel to `none`
    set style `display` of EditorPanel to `block`
    send ID to EditorModule
	stop

ViewRecord:
    rest get Index from `/index.json`
    or begin
    	put `[]` into Index
        continue
    end
    rest get Map from `/map.json`
    or begin
    	put `[]` into Map
        continue
    end
    
    if ID is `-`
    begin
    	if the json count of Index is 0 go to EditRecord
        get ID from storage as `current`
        if ID is empty go to Search
    end
    put ID into storage as `current`
    
	if ID is empty stop
    rest get Record from `/people/` cat ID cat `.json` or stop
    
    put property `fname` of Record into FamilyName
    put property `oname` of Record into OtherNames
    put property `gname` of Record into GivenName
    put empty into Mother
    put empty into Father
    
    clear Container
    create Title in Container
    set the style of Title to `text-align:center;margin-top:0`
    put GivenName into Text
    if OtherNames is not empty put Text cat ` ` cat OtherNames into Text
    put Text cat ` ` cat FamilyName into Text
    set the content of Title to Text
    
    put property `mother` of Record into MID
    if not MID go to VR2
    rest get Record2 from `/people/` cat MID cat `.json` or go to VR2
    put property `gname` of Record2 into GivenName
    put property `oname` of Record2 into OtherNames
    put property `fname` of Record2 into FamilyName
    put GivenName into Mother
    if OtherNames is not empty
    begin
    	if Mother is not empty put Mother cat ` ` into Mother
    	put Mother cat OtherNames into Mother
    end
    if FamilyName is not empty
    begin
    	if Mother is not empty put Mother cat ` ` into Mother
    	put Mother cat FamilyName into Mother
    end

VR2:
    put property `father` of Record into FID
    if not FID go to VR3
    rest get Record2 from `/people/` cat FID cat `.json` or go to VR3
    put property `gname` of Record2 into GivenName
    put property `oname` of Record2 into OtherNames
    put property `fname` of Record2 into FamilyName
    put GivenName into Father
    if OtherNames is not empty
    begin
    	if Father is not empty put Father cat ` ` into Father
    	put Father cat OtherNames into Father
    end
    if FamilyName is not empty
    begin
    	if Father is not empty put Father cat ` ` into Father
    	put Father cat FamilyName into Father
    end

VR3:
	if Mother cat Father is empty go to VR4
    create Para in Container
    if Mother is not empty
    begin
        create Label in Para
        set the content of Label to `Mother: `
    	create Link in Para
        set the content of Link to Mother
        on click Link
        begin
        	put MID into ID
            go to ViewAnotherRecord
        end
    end
    if Father is not empty
    begin
    	if Mother is not empty
        begin
	        create Label in Para
            set the content of Label to `&nbsp;&nbsp;&nbsp;`
        end
        create Label in Para
        set the content of Label to `Father: `
    	create Link in Para
        set the content of Link to Father
        on click Link
        begin
        	put FID into ID
            go to ViewAnotherRecord
        end
    end

VR4:
	put property ID of Map into Item
    put property `children` of Item into List
    if the json count of List is not 0
    begin
    	set the elements of Child to the json count of List
    	set the elements of ChildLink to the json count of List
    	create Para in Container
	    put 0 into N
	    while N is less than the json count of List
	    begin
        	if N is not 0
            begin
                create Label in Para
                set the content of Label to `&nbsp;&nbsp;&nbsp;`
            end
        	index Child to N
        	index ChildLink to N
	    	put element N of List into Child
	        rest get Record2 from `/people/` cat Child cat `.json` or go to VR5
	        put property `gname` of Record2 into GivenName
	        put property `gender` of Record2 into Gender
            create Label in Para
            if Gender is `m` set the content of Label to  `Son: `
            else if Gender is `f` set the content of Label to `Daughter: `
            else set the content of Label to `Child: `
            create ChildLink in Para
            set the content of ChildLink to GivenName
            on click ChildLink
            begin
            	index Child to the index of ChildLink
                put Child into ID
                go to ViewAnotherRecord
            end
	    	add 1 to N
	    end
    end

VR5:
	put property ID of Map into Item
    put property `siblings` of Item into List
    if the json count of List is not 0
    begin
    	set the elements of Sibling to the json count of List
    	set the elements of SiblingLink to the json count of List
    	create Para in Container
	    put 0 into N
	    while N is less than the json count of List
	    begin
        	index Sibling to N
        	index SiblingLink to N
	    	put element N of List into Sibling
	        rest get Record2 from `/people/` cat Sibling cat `.json` or go to DoButtons
	        put property `gname` of Record2 into GivenName
	        put property `fname` of Record2 into FamilyName
	        put property `gender` of Record2 into Gender
            create Label in Para
            if Gender is `m` set the content of Label to `Brother: `
            else if Gender is `f` set the content of Label to `Sister: `
            else set the content of Label to `Sibling: `
            create SiblingLink in Para
            set the content of SiblingLink to GivenName cat ` ` cat FamilyName
            on click SiblingLink
            begin
            	index Sibling to the index of SiblingLink
                put Sibling into ID
                go to ViewAnotherRecord
            end
	    	add 1 to N
	    end
    end
    stop

ViewAnotherRecord:
    clear Container
    clear Father
    clear Mother
	if Editable set style `display` of EditButton to `block`
	set style `display` of SearchButton to `block`
    go to ViewRecord

Search:
	if Editable set style `display` of EditButton to `none`
	set style `display` of SearchButton to `none`
    create Fieldset in Container
    create Legend in Fieldset
    set the content of Legend to `Select a record`
    set the style of Fieldset to `padding 0.5em`
    create Row in Fieldset
    set style `padding-left` of Row to `5em`
    create Para in Row
    set the content of Para to
    	`Type the full name or the first part of either the given or the family name.`
        cat break cat `Or leave the field blank to get all the records.`
    create Row in Fieldset
    set the style of Row to `display:flex`
    create Label in Row
    set style `width` of Label to `5em`
    set the content of Label to `Name:&nbsp;&nbsp;`
    create Name in Row
    set the style of Name to `flex:1;padding:0.25em`
    create FindButton in Row
    set style `margin-left` of FindButton to `1em`
    set the text of FindButton to `Find person`
    create CancelButton in Row
    set the style of CancelButton to `margin-left:1em`
    set the text of CancelButton to `Cancel`
    on click CancelButton
    begin
    	remove element Fieldset
		if Editable set style `display` of EditButton to `block`
		set style `display` of SearchButton to `block`
        stop
    end
    create Choices in Fieldset
    focus Name
    on click FindButton go to S2
    on key
    begin
    	if the key is `Enter` go to S2
    end
    stop

S2:
    put 0 into N
    put `[]` into Selection
    while N is less than the json count of Index
    begin
    	put element N of Index into ID
        rest get Record from `/people/` cat ID cat `.json`
        put property `fname` of Record into FamilyName
        put property `gname` of Record into GivenName
        if Name is GivenName cat ` ` cat FamilyName
        begin
        	json add N to Selection
           	go to SR2
        end
        if the position nocase of Name in FamilyName is 0 json add ID to Selection
        else if the position nocase of Name in GivenName is 0 json add ID to Selection
SR2:
    	add 1 to N
    end

    put the json count of Selection into N
    if N is greater than 0
    begin
    	if N is 1
        begin
        	put element 0 of Selection into ID
            go to ViewAnotherRecord
        end
        ! Show a list of matching results
        clear Choices
        set the style of Choices to `margin-left:5em;margin-top:1em`
        set the elements of ChoiceButton to the json count of Selection
        set the elements of ChoiceIndex to the json count of Selection
        put 0 into N
        while N is less than the json count of Selection
        begin
        	put element N of Selection into ID
    		rest get Record from `/people/` cat ID cat `.json`
            put property `fname` of Record into FamilyName
            put property `gname` of Record into GivenName
            put property `dob` of Record into Date
            if N is not 0
            begin
            	create Label in Choices
                set the content of Label to `&nbsp;&nbsp;&nbsp;`
            end
            index ChoiceButton to N
            create ChoiceButton in Choices
            put GivenName cat ` ` cat FamilyName into Text
            if Date is not empty put Text cat ` (` cat Date cat `)` into Text
            set the text of ChoiceButton to Text
            index ChoiceIndex to N
           	put ID into ChoiceIndex
        	add 1 to N
        end
        on click ChoiceButton
        begin
        	put the index of ChoiceButton into N
            remove element Fieldset
            index ChoiceIndex to N
            put ChoiceIndex into ID
            go to ViewAnotherRecord
        end
       	stop
    end
    alert `No matching record(s) found.`
    stop